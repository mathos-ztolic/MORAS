class QuickSort {

	function void swap(Array arr, int i, int j) {
		var int temp;
		let temp = arr[i];
		let arr[i] = arr[j];
		let arr[j] = temp;
		return;
	}

	function int partition(Array arr, int l, int h) {
		var int pivot;
		var int i, j;
		let pivot = arr[h];

		let i = l - 1;
		let j = l;
		while (~(j > h)) {
			if (arr[j] < pivot) { let i = i + 1; do QuickSort.swap(arr, i, j); }
			let j = j + 1;
		}
		do QuickSort.swap(arr, i + 1, h);
		return i + 1;
	}

	function void quicksort(Array arr, int first_index, int last_index) {
		var int pivot_index;
		if (first_index > last_index) { return; }
		let pivot_index = QuickSort.partition(arr, first_index, last_index);
		do QuickSort.quicksort(arr, first_index, pivot_index - 1);
		do QuickSort.quicksort(arr, pivot_index + 1, last_index);
		return;
	}

}
