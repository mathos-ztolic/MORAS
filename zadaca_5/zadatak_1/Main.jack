class Main {

	function void swap(Array arr, int i, int j) {
		var int temp;
		let temp = arr[i];
		let arr[i] = arr[j];
		let arr[j] = temp;
		return;
	}

	function int partition(Array arr, int l, int h) {
		var int pivot;
		var int i, j;
		let pivot = arr[h];

		let i = l - 1;
		let j = l;
		while (~(j > h)) {
			if (arr[j] < pivot) { let i = i + 1; do Main.swap(arr, i, j); }
			let j = j + 1;
		}
		do Main.swap(arr, i + 1, h);
		return i + 1;
	}

	function void qsort(Array arr, int first_index, int last_index) {
		var int pivot_index;
		if (first_index > last_index) { return; }
		let pivot_index = Main.partition(arr, first_index, last_index);
		do Main.qsort(arr, first_index, pivot_index - 1);
		do Main.qsort(arr, pivot_index + 1, last_index);
		return;
	}

	function void main() {
		var Array arr;
		var int length;
		var int i;

		let length = Keyboard.readInt("Broj elemenata: ");
		if (length = 0) { return; }
		let arr = Array.new(length);
		let i = 0;
		while (i < length) {
			do Output.printString("Unesite ");
			do Output.printInt(i+1);
			let arr[i] = Keyboard.readInt(". broj: ");
			let i = i+1;
		}
		do Main.qsort(arr, 0, length-1);	
		let i = 0;
		while (i < (length - 1)) {
			do Output.printInt(arr[i]);
			do Output.printString(", ");
			let i = i + 1;
		}
		do Output.printInt(arr[i]);
		do arr.dispose();
		return;
	}

}
